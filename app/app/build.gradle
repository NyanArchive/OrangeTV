plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

task genDex {}

tasks.register('copyAppDex', Copy) {
    from layout.buildDirectory.file("/intermediates/dex/debug/mergeLibDexDebug/0/classes.dex")
    into layout.projectDirectory.dir("dex")
    rename {
        "app.dex"
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'mergeLibDexDebug') {
        task.doFirst {
            delete fileTree("$rootProject.projectDir/Twitch/build/.transforms") { include '**/*.dex' }
            delete fileTree("$rootProject.projectDir/TwitchRes/build/.transforms") { include '**/*.dex' }
        }
    }
}

genDex.dependsOn 'mergeLibDexDebug'
genDex.finalizedBy 'copyAppDex'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "tv.orange"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    api project(path: ':bridge')
}